defmodule Pantheon.Data.UserDB do
  alias Pantheon.Data.DbHelpers

  @datetime_columns [:inserted_at, :updated_at]

  def schema do
    Zoi.object(%{
      id: Zoi.uuid(),
      email: Zoi.string(),
      inserted_at: Zoi.datetime(),
      updated_at: Zoi.datetime()
    })
  end

  def find_or_create(email) do
    sql = """
    INSERT INTO users (email)
    VALUES ($(email))
    ON CONFLICT (email) DO UPDATE SET updated_at = NOW()
    RETURNING id, email, inserted_at, updated_at
    """

    case DbHelpers.run_sql(sql, %{"email" => email})
         |> DbHelpers.rows_apply_datetime_conversion(@datetime_columns)
         |> DbHelpers.validate_rows(schema()) do
      {:error, reason} -> {:error, reason}
      [] -> {:error, :not_found}
      [user | _] -> {:ok, user}
    end
  end

  def get_by_id(user_id) do
    sql = """
    SELECT id, email, inserted_at, updated_at
    FROM users
    WHERE id = $(user_id)
    """

    case DbHelpers.run_sql(sql, %{"user_id" => user_id})
         |> DbHelpers.rows_apply_datetime_conversion(@datetime_columns)
         |> DbHelpers.validate_rows(schema()) do
      [user | _] -> {:ok, user}
      [] -> {:error, :not_found}
      {:error, reason} -> {:error, reason}
    end
  end
end
